task npmInstall(type: Exec) {
    inputs.file("${rootDir}/frontend/package.json")
    outputs.dir("${rootDir}/frontend/node_modules")
    outputs.file("${rootDir}/frontend/package-lock.json")

    workingDir "${rootDir}/frontend"
    commandLine 'npm', 'install'
}

task npmBuild(type: Exec) {
    dependsOn(npmInstall)
    inputs.dir("${rootDir}/frontend/src")
    inputs.dir("${rootDir}/frontend/public")
    outputs.dir("${rootDir}/frontend/dist")

    workingDir "${rootDir}/frontend"
    commandLine 'npm', 'run', 'build:prod'
}

task copyJacocoAgentJar(type: Copy) {
    def jacocoAgentJarName = "org.jacoco.agent-${jacocoVersion}.jar"
    def jacocoAgentJar = configurations.getByName('jacocoAgent').filter {
        it.name == jacocoAgentJarName
    }
    if (jacocoAgentJar.empty) {
        throw new FileNotFoundException("Can't find the file $jacocoAgentJarName")
    }
    from(zipTree(jacocoAgentJar.singleFile)) {
        include 'jacocoagent.jar'
    }
    into "${project.buildDir}/jacocoagent"
}

task assembleApp() {
    dependsOn(npmBuild, copyJacocoAgentJar)
    finalizedBy(assemble)
}